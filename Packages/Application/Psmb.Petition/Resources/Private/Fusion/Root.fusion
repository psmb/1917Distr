prototype(Psmb.Petition:SignPlugin) < prototype(Neos.Fusion:Value) {
	value = Neos.Fusion:Array {
		1 = Plugin {
			package = 'Psmb.Petition'
			controller = 'Sign'
			argumentNamespace = 'sign-petition'
		}
		2 = '<h2 class="t-title t-title_sm">К акции присоединились:</h2>'
		3 = Psmb.Petition:List
	}
}

prototype(Psmb.Petition:List) < prototype(Neos.Fusion:Collection) {
	@process.tmpl = ${'<div class="SignatureList">' + value + '</div>'}
	itemName = 'node'
	collection = ${Array.reverse(q(documentNode).children('petitions').children('[instanceof Psmb.Petition:Signature]').get())}
	itemRenderer = Psmb.Petition:Signature

	@cache {
		mode = 'cached'
		entryIdentifier.node = ${node}
		entryTags.1 = 'NodeType_Psmb.Petition:Signature'
	}
}

prototype(Psmb.Petition:Signature) {
	name = Neos.Fusion:Tag {
		tagName = 'span'
		content = ${q(node).property('name')}
		@process.contentElementEditable = ContentElementEditable {
			property = 'name'
		}
	}
	place = Neos.Fusion:Tag {
		tagName = 'span'
		content = ${q(node).property('place')}
		@process.contentElementEditable = ContentElementEditable {
			property = 'place'
		}
	}
	about = Neos.Fusion:Tag {
		tagName = 'span'
		content = ${q(node).property('about')}
		@process.contentElementEditable = ContentElementEditable {
			property = 'about'
		}
	}
}

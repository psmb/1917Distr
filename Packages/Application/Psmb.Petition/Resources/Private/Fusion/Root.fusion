prototype(Psmb.Petition:SignaturesQuery) < prototype(Neos.Fusion:Value) {
	value = ${q(documentNode).children('petitions').children('[instanceof Psmb.Petition:Signature]')}
}

prototype(Psmb.Petition:SignPlugin) < prototype(Neos.Fusion:Value) {
	value = Neos.Fusion:Array {
		@context.count =  Psmb.Petition:SignaturesQuery
		@context.count.@process.count = ${value.count()}
		10 = ${'<h2 class="t-title t-title_sm">' + Translation.translate('petitionSigned', null, [], null, 'Psmb.Petition') + '</h2>'}
		20 = Neos.Fusion:Collection {
			@process.tmpl = ${'<div class="SignatureList">' + value + '</div>'}
			@process.topPetitions = Psmb.Ajaxify:Ajaxify
			itemName = 'node'
			collection = Psmb.Petition:SignaturesQuery
			collection.@process.filter = ${value.filter('[vip = TRUE]').get()}
			itemRenderer = Psmb.Petition:Signature
			@cache {
				mode = 'cached'
				entryIdentifier.node = ${node}
				entryTags.1 = 'NodeType_Psmb.Petition:Signature'
			}
		}
		30 = Plugin {
			package = 'Psmb.Petition'
			controller = 'Sign'
			argumentNamespace = 'sign-petition'
		}
		40 = ${'<h2 class="t-title t-title_sm">' + Translation.translate('numberJoined', null, [count], null, 'Psmb.Petition', count) + '</h2>'}
		50 = Sfi.Site:Carousel {
			@context.signatures = Psmb.Petition:SignaturesQuery
			isWhite = ${false}
			showDots = ${false}
			showArrows = ${true}
			slides = Neos.Fusion:Collection {
				signatures = Psmb.Petition:SignaturesQuery
				collection = ${count > 0 && Array.reverse(signatures.filter('[selected = TRUE]').get())}
				itemName = 'node'
				itemRenderer = Sfi.Site:CarouselSlide {
					content = Psmb.Petition:SignatureSelected {
						@process.tm = ${'<div style="padding: 0 50px">' + value + '</div>'}
					}
					@process.contentElementWrapping>
				}
			}
			slidesNav.collection = ${count > 0 && Array.reverse(signatures.filter('[selected = TRUE]').get())}
			@process.tmpl = ${'<div class="">' + value + '</div>'}
		}
		60 = Neos.Fusion:Collection {
			@process.tmpl = ${'<div class="SignatureList">' + value + '</div>'}
			@process.allPetitions = Psmb.Ajaxify:Ajaxify
			itemName = 'node'
			collection = Psmb.Petition:SignaturesQuery
			collection.@process.filter = ${Array.reverse(value.filter('[vip != TRUE]').get())}
			itemRenderer = Psmb.Petition:Signature
			@cache {
				mode = 'cached'
				entryIdentifier.node = ${node}
				entryTags.1 = 'NodeType_Psmb.Petition:Signature'
			}
		}
	}
	@cache {
		mode = 'cached'
		entryIdentifier.node = ${node}
		entryTags.1 = 'NodeType_Psmb.Petition:Signature'
	}
}


prototype(Psmb.Petition:SignatureSelected) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Psmb.Petition/Private/Templates/NodeTypes/SignatureSelected.html'
	name = ${node.properties.name}
	place = ${node.properties.place}
	about = ${node.properties.about}
	comment = ${node.properties.comment}
}

prototype(Psmb.Petition:Signature) {
	name = ${q(node).property('name')}
	place = ${q(node).property('place')}
	about = ${q(node).property('about')}
	comment = ${q(node).property('comment')}
	inBackend = ${node.context.inBackend}
}


root.export {
	condition = ${request.arguments.export == '1'}
	renderer = Neos.Fusion:Http.Message {
		httpResponseHead.headers.Content-Type = 'text/csv'
		httpResponseHead.headers.Content-Disposition = 'attachment;filename=pokayanie1917-signatures.xml'
		1 = Neos.Fusion:Template {
			templatePath = 'resource://Psmb.Petition/Private/Templates/NodeTypes/SignatureExport.html'
			items = Neos.Fusion:Collection {
				collection = ${q(documentNode).children('petitions').children('[instanceof Psmb.Petition:Signature]').get()}
				itemName = 'node'
				itemRenderer = Psmb.Petition:SignatureSelected {
					templatePath = 'resource://Psmb.Petition/Private/Templates/NodeTypes/SignatureExportItem.html'
					email = ${node.properties.email}
				}
			}
		}
	}
}
root.@cache.entryIdentifier.export = ${request.arguments.export}

prototype(Psmb.Petition:SignPlugin) < prototype(Neos.Fusion:Value) {
	@context.signatures = ${q(documentNode).children('petitions').children('[instanceof Psmb.Petition:Signature]')}
	value = Neos.Fusion:Array {
		@context.count = ${signatures.count()}
		1 = Plugin {
			package = 'Psmb.Petition'
			controller = 'Sign'
			argumentNamespace = 'sign-petition'
		}
		2 = ${'<h2 class="t-title t-title_sm">' + Translation.translate('numberJoined', null, [count], null, 'Psmb.Petition', count) + '</h2>'}
		3 = Sfi.Site:Carousel {
			isWhite = ${false}
			showDots = ${true}
			showArrows = ${true}
			slides = Neos.Fusion:Collection {
				collection = ${count > 0 && signatures.filter('[selected = TRUE]').get()}
				itemName = 'node'
				itemRenderer = Sfi.Site:CarouselSlide {
					attributes.style = 'height:400px'
					content = Psmb.Petition:SignatureSelected {
						@process.tm = ${'<div style="padding: 0 50px">' + value + '</div>'}
					}
					@process.contentElementWrapping>
				}
			}
			slidesNav.collection = ${count > 0 && signatures.filter('[selected = TRUE]').get()}
			@process.tmpl = ${'<div class="">' + value + '</div>'}
		}
		4 = Neos.Fusion:Collection {
			@process.tmpl = ${'<div class="SignatureList">' + value + '</div>'}
			itemName = 'node'
			collection = ${Array.reverse(signatures.get())}
			itemRenderer = Psmb.Petition:Signature
		}
	}
	@cache {
		mode = 'cached'
		entryIdentifier.node = ${node}
		entryTags.1 = 'NodeType_Psmb.Petition:Signature'
	}
}


prototype(Psmb.Petition:SignatureSelected) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Psmb.Petition/Private/Templates/NodeTypes/SignatureSelected.html'
	name = ${node.properties.name}
	place = ${node.properties.place}
	about = ${node.properties.about}
	comment = ${node.properties.comment}
}

prototype(Psmb.Petition:Signature) {
	name = Neos.Fusion:Tag {
		tagName = 'span'
		content = ${q(node).property('name')}
		@process.contentElementEditable = ContentElementEditable {
			property = 'name'
		}
	}
	place = Neos.Fusion:Tag {
		tagName = 'span'
		content = ${q(node).property('place')}
		@process.contentElementEditable = ContentElementEditable {
			property = 'place'
		}
	}
	about = Neos.Fusion:Tag {
		tagName = 'span'
		content = ${q(node).property('about')}
		@process.contentElementEditable = ContentElementEditable {
			property = 'about'
		}
	}
	comment = Neos.Fusion:Tag {
		tagName = 'span'
		content = ${q(node).property('comment')}
		@process.contentElementEditable = ContentElementEditable {
			property = 'comment'
		}
	}
	inBackend = ${node.context.inBackend}
}
